SET NOCOUNT ON

DECLARE @PREFIXO VARCHAR(10) = ''

DECLARE
	@T_NAME VARCHAR(255),
	@I_NAME VARCHAR(255),
	@C_NAME VARCHAR(255),
	@C_POS INT,
	@C_DRUL VARCHAR(255),
	@C_URUL VARCHAR(255),
	@U_CONS VARCHAR(255) = '',
	@C_1ST BIT = 1,
	@DI_NAME VARCHAR(255),
	@DT_NAME VARCHAR(255),
	@DC_NAME VARCHAR(255),
	@DC_POS INT,		
	@DELETE_RULE VARCHAR(255),
	@UPDATE_RULE VARCHAR(255),
	@LINE_ALTER VARCHAR(4000),
	@LINE_REFER VARCHAR(4000)
	
DECLARE	@T TABLE(LINE VARCHAR(MAX))

DECLARE CUR_CONSTRAINTS CURSOR FAST_FORWARD FOR
SELECT  
     KCU1.CONSTRAINT_NAME AS FK_CONSTRAINT_NAME 
    ,KCU1.TABLE_NAME AS FK_TABLE_NAME 
    ,KCU1.COLUMN_NAME AS FK_COLUMN_NAME 
    ,KCU1.ORDINAL_POSITION AS FK_ORDINAL_POSITION 
    ,KCU2.CONSTRAINT_NAME AS REFERENCED_CONSTRAINT_NAME 
    ,KCU2.TABLE_NAME AS REFERENCED_TABLE_NAME 
    ,KCU2.COLUMN_NAME AS REFERENCED_COLUMN_NAME 
    ,KCU2.ORDINAL_POSITION AS REFERENCED_ORDINAL_POSITION
    ,RC.DELETE_RULE
    ,RC.UPDATE_RULE
FROM INFORMATION_SCHEMA.REFERENTIAL_CONSTRAINTS AS RC 

INNER JOIN INFORMATION_SCHEMA.KEY_COLUMN_USAGE AS KCU1 
    ON KCU1.CONSTRAINT_CATALOG = RC.CONSTRAINT_CATALOG  
    AND KCU1.CONSTRAINT_SCHEMA = RC.CONSTRAINT_SCHEMA 
    AND KCU1.CONSTRAINT_NAME = RC.CONSTRAINT_NAME 

INNER JOIN INFORMATION_SCHEMA.KEY_COLUMN_USAGE AS KCU2 
    ON KCU2.CONSTRAINT_CATALOG = RC.UNIQUE_CONSTRAINT_CATALOG  
    AND KCU2.CONSTRAINT_SCHEMA = RC.UNIQUE_CONSTRAINT_SCHEMA 
    AND KCU2.CONSTRAINT_NAME = RC.UNIQUE_CONSTRAINT_NAME 
    AND KCU2.ORDINAL_POSITION = KCU1.ORDINAL_POSITION
WHERE (ISNULL(@PREFIXO, '') = '' OR KCU1.TABLE_NAME LIKE '%' + @PREFIXO + '%')

OPEN CUR_CONSTRAINTS

FETCH NEXT FROM CUR_CONSTRAINTS INTO 
	@I_NAME,
	@T_NAME,
	@C_NAME,
	@C_POS,
	@DI_NAME,
	@DT_NAME,
	@DC_NAME,
	@DC_POS,		
	@C_DRUL,
	@C_URUL

WHILE @@FETCH_STATUS = 0
BEGIN


	IF(@I_NAME <> @U_CONS)
	BEGIN
		IF(@U_CONS <> '')	
		BEGIN
			INSERT INTO @T(LINE) VALUES(@LINE_ALTER + ')')
			INSERT INTO @T(LINE) VALUES(@LINE_REFER + ')')
			INSERT INTO @T(LINE) VALUES(@DELETE_RULE)
			INSERT INTO @T(LINE) VALUES(@UPDATE_RULE)
			INSERT INTO @T(LINE) VALUES('GO')
		END
		
		INSERT INTO @T(LINE) VALUES('IF(NOT EXISTS(SELECT 1 FROM SYSOBJECTS WHERE ID = OBJECT_ID(''' + @I_NAME + ''')))')
			
		
		SET @LINE_ALTER = 'ALTER TABLE [' + @T_NAME + '] ADD CONSTRAINT [' +  @I_NAME + '] FOREIGN KEY ('
		SET @LINE_REFER = 'REFERENCES [' + @DT_NAME + '] ('		
		
		SET @C_1ST = 1
	END


	SET @LINE_ALTER = @LINE_ALTER + CASE WHEN @C_1ST = 0 THEN ',' ELSE '' END + @C_NAME
	SET @LINE_REFER = @LINE_REFER + CASE WHEN @C_1ST = 0 THEN ',' ELSE '' END + @DC_NAME		
	
	SET @DELETE_RULE = 'ON DELETE ' + @C_DRUL
	
	SET @UPDATE_RULE = 'ON UPDATE ' + @C_URUL
	
	SET @C_1ST = 0
	
	SET @U_CONS = @I_NAME
	
FETCH NEXT FROM CUR_CONSTRAINTS INTO 
	@I_NAME,
	@T_NAME,
	@C_NAME,
	@C_POS,
	@DI_NAME,
	@DT_NAME,
	@DC_NAME,
	@DC_POS,		
	@C_DRUL,
	@C_URUL

END
CLOSE CUR_CONSTRAINTS
DEALLOCATE CUR_CONSTRAINTS

INSERT INTO @T(LINE) VALUES(@LINE_ALTER + ')')
INSERT INTO @T(LINE) VALUES(@LINE_REFER + ')')
INSERT INTO @T(LINE) VALUES(@DELETE_RULE)
INSERT INTO @T(LINE) VALUES(@UPDATE_RULE)
INSERT INTO @T(LINE) VALUES('GO')


SELECT LINE FROM @T

SET NOCOUNT OFF