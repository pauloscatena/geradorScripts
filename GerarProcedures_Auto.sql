
SET NOCOUNT ON;
DECLARE @PREFIXO VARCHAR(10) = 'OPF'

DECLARE @PROC_NAME VARCHAR(500),
		@ROUT_TYPE VARCHAR(50),
		@ROUT_SCHEMA VARCHAR(50)

CREATE TABLE #t (ID BIGINT IDENTITY(1,1),  line varchar(max))

DECLARE CUR_PROCS CURSOR FAST_FORWARD FOR
SELECT DISTINCT SPECIFIC_NAME, ROUTINE_TYPE, ROUTINE_SCHEMA FROM INFORMATION_SCHEMA.ROUTINES
WHERE SPECIFIC_NAME LIKE '%' + @PREFIXO + '%'
AND ROUTINE_TYPE = 'PROCEDURE'

OPEN CUR_PROCS
FETCH NEXT FROM CUR_PROCS INTO @PROC_NAME, @ROUT_TYPE, @ROUT_SCHEMA
WHILE @@FETCH_STATUS = 0
BEGIN

	INSERT INTO #T(line) VALUES('/*Início da ' + @ROUT_TYPE + ' ' + @PROC_NAME + '*/')
	INSERT INTO #T(line) VALUES('IF(EXISTS(SELECT 1 FROM SYSOBJECTS WHERE ID = OBJECT_ID(''' + @PROC_NAME + ''')))')
	INSERT INTO #T(line) VALUES('DROP ' + @ROUT_TYPE + ' ' + @ROUT_SCHEMA + '.' + @PROC_NAME )
	INSERT INTO #T(line) VALUES('GO') 
	   
	INSERT INTO #T
	EXEC sp_helptext @PROC_NAME;

	INSERT INTO #T(line) VALUES('GO')
	INSERT INTO #T(line) VALUES('/* Fim da ' + @ROUT_TYPE + ' ' + @PROC_NAME +  '*/')

	INSERT INTO #T(line) VALUES('/* + ' + CONVERT(VARCHAR(20), getdate()) + '  */')

	FETCH NEXT FROM CUR_PROCS INTO @PROC_NAME, @ROUT_TYPE, @ROUT_SCHEMA

END
CLOSE CUR_PROCS
DEALLOCATE CUR_PROCS

SELECT line 
FROM #T
ORDER BY ID

DROP TABLE #T
SET NOCOUNT OFF